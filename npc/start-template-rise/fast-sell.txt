prontera,158,188,4	script	ขายของเร็ว	84,{
	menu
	"-",-,
	"-",-,
	"-",-,
	"ขายของเร็ว (Item ที่ {Favourite อยู่ / สวมใส่อยู่ / เป็น Item พิเศษ} จะไม่ถูกขาย)",L_FastSell
	;
	end;
	
	L_FastSell:
	getinventorylist;
	.@ocLv = getskilllv(38);
	.@ocRate = ((.@ocLv > 0) ? min(24,(4 + (3 * .@ocLv))) : 0);
	for(.@i = 0; .@i < @inventorylist_count; .@i++){
		if(@inventorylist_favorite[.@i] || @inventorylist_equip[.@i])
		continue;
		
		.@itemId = @inventorylist_id[.@i];
		.@isRestrictItemId = 0;
		for(.@j = 0; .@j < getarraysize(.restrictedItemId); .@j++){
			if(.@itemId == .restrictedItemId[.@j]){
				.@isRestrictItemId = 1;
				break;
			}
		}
		
		if(.@isRestrictItemId)
		continue;
		
		.@itemAmount = @inventorylist_amount[.@i];
		
		.@itemSellPrice = getiteminfo(.@itemId,ITEMINFO_SELL);
		.@ocPrice = ((.@itemSellPrice / 100) * .@ocRate);
		.@itemSellPrice = ((.@itemSellPrice + .@ocPrice) * .@itemAmount);
		
		if(IsZenyExceed(.@itemSellPrice))
		goto L_Summary;
		
		delitemidx @inventorylist_idx[.@i];
		
		ChangeZeny(.@itemSellPrice);
		
		.@summaryZeny = Cap(.@summaryZeny,.@itemSellPrice);
	}
	L_Summary:
	message strcharinfo(0),"ได้รับ " + F_InsertComma(.@summaryZeny) + "z";
	end;
	
OnInit:
	setarray .restrictedItemId[0],500;
	end;
}
function	script	InputOutOfRange	{ return (getarg(0,0) <= 0) || (getarg(0,0) > getarg(1,0)); }

function	script	IsWeekend	{ .@day = gettime(DT_DAYOFWEEK); return (((.@day == FRIDAY) && gettime(DT_HOUR) >= 18) || (.@day == SATURDAY) || (.@day == SUNDAY)); }

function	script	IsInventoryFull	{ return (!checkweight(1100,1)); }

function	script	StringBase10000	{
	.@value = getarg(0,0);
	if(.@value < 10)
	return ("0.0" + .@value);
	else if(.@value < 100)
	return ("0." + (.@value / 10));
	else if(.@value < 1000){
		.@s$ = "" + (.@value / 10) + "";
		return insertchar(.@s$,".",1);
	}
	else
	return (.@value /= 100);
}

function	script	KM	{
	.@value = getarg(0,0);
	if(.@value < 1000)
	return .@value;
	else if(.@value < 1000000)
	return (.@value /= 1000) + "k";
	else if(.@value >= 1000000)
	return (.@value /= 1000000) + "m";
}

function	script	Cap	{ return cap_value(getarg(0,0) + getarg(1,0),0,INT_MAX); }

function	script	IsZenyExceed	{
	.@isZenyExceed = (Cap(Zeny,getarg(0,0)) > 2000000000);
	if(.@isZenyExceed)
	message strcharinfo(0),"เกิดข้อผิดพลาด: Zeny เต็มแล้ว";
	return .@isZenyExceed;
}

function	script	ChangeZeny	{
	.@value = getarg(0,0);
	.@isEffect = getarg(1,1);
	.@isIncrease = (.@value >= 0);
	Zeny = Cap(Zeny,.@value);
	message strcharinfo(0),(.@isIncrease ? "+" : "-") + F_InsertComma(.@value) + "z เป็น " + F_InsertComma(Zeny) + "z";
	if(.@isEffect)
	specialeffect2 18;
	return;
}

function	script	Storage	{
	.@menu$ = "คลังเก็บของปกติ:คลังเก็บของ Guild:";
	.@selectedStorage = select(.@menu$);
	if(.@selectedStorage == 1)
	openstorage;
	else if(.@selectedStorage == 2)
	guildopenstorage();
	return;
}

function	script	ReturnOrb	{
	warp "Save",0,0;
	return;
}

function	script	TeleportOrb	{
	@tGID = 0;
	warp "Random",0,0;
	return;
}

function	script	ApplyEffect	{
	bonus_script_clear 8;
	
	if(tired < 100){
		bonus_script
		"bonus bBaseAtk," + cap_value(sharpeningStone,0,30000) + ";"
		+ "bonus bAgi," + cap_value(hasteStone,0,30000) + ";"
		+ "bonus bAspd," + (effectStone / 1000) + ";"
		+ "bonus2 bAddClass,CLASS_ALL," + effectStone + ";"
		,86400,8,1;
	}
	
	return;
}

function	script	SharpeningStone	{
	.@itemId = 20000002 + (getarg(0,1) - 1);
	.@useAmount = !isSkipUseAll ? countitem(.@itemId) : 1;
	sharpeningStone = cap_value(sharpeningStone + ($sharpeningStone[getarg(0,1) - 1] * .@useAmount),0,30000);
	dispbottom "[Sharpening Stone] ขณะนี้ ATK +" + sharpeningStone + " (จะลดลง 1 ทุก ๆ 10 วินาทีที่อยู่ในเหมือง)";
	delitem .@itemId,.@useAmount;
	callfunc("ApplyEffect");
	return;
}

function	script	HasteStone	{
	.@itemId = 20000012 + (getarg(0,1) - 1);
	.@useAmount = !isSkipUseAll ? countitem(.@itemId) : 1;
	hasteStone = cap_value(hasteStone + ($hasteStone[getarg(0,1) - 1] * .@useAmount),0,30000);
	dispbottom "[Haste Stone] ขณะนี้ AGI +" + hasteStone + " (จะลดลง 1 ทุก ๆ 10 วินาทีที่อยู่ในเหมือง)";
	delitem .@itemId,.@useAmount;
	callfunc("ApplyEffect");
	return;
}

function	script	SkillStone	{
	.@itemId = 20000022 + (getarg(0,1) - 1);
	.@useAmount = !isSkipUseAll ? countitem(.@itemId) : 1;
	
	freeloop(1);
	for(.@i = 0; .@i < .@useAmount; .@i++){
		delitem .@itemId,1;
		
		.@chance = $skillStone[getarg(0,1) - 1];
		.@rand = rand(10000);
		
		if(.@rand <= .@chance){
			.@skillType = rand(3);
			
			if(.@skillType == 0)
			bonus_script "bonus2 bAddClass,CLASS_ALL," + $skillSuperDamage + ";",$skillDuration,1,1,26;
			else if(.@skillType == 1)
			bonus_script "bonus bAspd," + $skillSuperFast + ";",$skillDuration,1,1,39;
			else
			bonus_script "bonus bSplashRange," + $skillSuperSplash + ";",$skillDuration,1,1,86;
			
			message strcharinfo(0),$skName$[.@skillType] + "!";
			
			// Stop the loop
			break;
		}
		else
		dispbottom "[Skill Stone] ต้องสุ่มได้น้อยกว่า " + .@chance + " คุณสุ่มได้ " + .@rand;
	}
	freeloop(0);
	
	return;
}

function	script	EffectStone	{
	.@itemId = 20000032 + (getarg(0,1) - 1);
	.@useAmount = !isSkipUseAll ? countitem(.@itemId) : 1;
	effectStone = cap_value(effectStone + ($effectStone[getarg(0,1) - 1] * .@useAmount),0,100000);
	dispbottom "[Effect Stone] ขณะนี้ Effect Lv. " + effectStone + "/100000";
	delitem .@itemId,.@useAmount;
	callfunc("ApplyEffect");
	return;
}
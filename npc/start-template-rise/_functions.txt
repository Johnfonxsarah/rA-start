function	script	InputOutOfRange	{ return getarg(0,0) <= 0 || getarg(0,0) > getarg(1,0); }

function	script	IsWeekend	{ .@day = gettime(DT_DAYOFWEEK); return (((.@day == FRIDAY) && gettime(DT_HOUR) >= 18) || (.@day == SATURDAY) || (.@day == SUNDAY)); }

function	script	IsInventoryFull	{ return (!checkweight(1100,1)); }

function	script	ApplyTired	{
	atcommand "@fakename";
	if(tired)
	.@debuffHint$ = "ล้า " + tired + "%";
	if(bonusDebuff)
	.@debuffHint$ += ((.@debuffHint$ != "") ? " " : "") + "ของเสื่อม " + bonusDebuff + "%";
	if(.@debuffHint$ != "")
	atcommand "@fakename [" + .@debuffHint$ + "]" + strcharinfo(0);
	atcommand "@debuff " + tired + " " + bonusDebuff;
	return;
}

function	script	ResetTired	{
	tired = 0;
	tiredSpot = 0;
	ptt = 0;
	ApplyTired();
	return;
}

function	script	IsSkipTickMap	{
	.@value$ = getarg(0,strcharinfo(3));
	return ((.@value$ == "bl_depth2") || (.@value$ == "1@vrsn"));
}

function	script	StringBase10000	{
	.@value = getarg(0,0);
	if(.@value < 10)
	return ("0.0" + .@value);
	else if(.@value < 100)
	return ("0." + (.@value / 10));
	else if(.@value < 1000)
	return insertchar((.@value / 10),".",1);
	else
	return (.@value /= 100);
}

function	script	KM	{
	.@value = getarg(0,0);
	if(.@value < 1000)
	return .@value;
	else if(.@value < 1000000)
	return (.@value /= 1000) + "k";
	else if(.@value >= 1000000)
	return (.@value /= 1000000) + "m";
}

function	script	Cap	{ return cap_value(getarg(0,0) + getarg(1,0),0,INT_MAX); }

function	script	IsZenyExceed	{
	.@isZenyExceed = (Cap(Zeny,getarg(0,0)) > 2000000000);
	if(.@isZenyExceed)
	message strcharinfo(0),"เกิดข้อผิดพลาด: Zeny เต็มแล้ว";
	return .@isZenyExceed;
}

function	script	ChangeZeny	{
	.@value = getarg(0,0);
	.@isEffect = getarg(1,1);
	.@isIncrease = (.@value >= 0);
	Zeny = Cap(Zeny,.@value);
	message strcharinfo(0),(.@isIncrease ? "+" : "-") + F_InsertComma(.@value) + "z เป็น " + F_InsertComma(Zeny) + "z";
	if(.@isEffect)
	specialeffect2 18;
	return;
}

function	script	Storage	{
	.@isFree = 1;
	.@menu$ = "คลังเก็บของปกติ:คลังเก็บของ Guild:";
	for(.@s = 0; .@s < 9; .@s++)
	.@menu$ += "คลังเก็บของพิเศษ " + (.@s + 1) + ":";
	.@selectedStorage = select(.@menu$);
	if(.@selectedStorage == 1)
	openstorage;
	else if(.@selectedStorage == 2)
	guildopenstorage();
	else{
		.@storageId = (.@selectedStorage - 2);
		if(getd("#STORAGE" + .@storageId) || .@isFree)
		openstorage2 .@storageId,STOR_MODE_GET|STOR_MODE_PUT;
		else{
			.@price = (10000000 * .@storageId);
			menu
			"ยกเลิก",-
			,"ซื้อคลังเก็บของพิเศษ " + .@storageId + " (ใช้ได้ทั้ง ID) [^af1ada" + F_InsertComma(.@price) + "z^000000]",L_Buy;
			end;
			
			L_Buy:
			if(Zeny < .@price){
				mes "Zeny ไม่เพียงพอ";
				close;
			}
			ChangeZeny(-.@price);
			set getd("#STORAGE" + .@storageId),1;
			openstorage2 .@storageId,STOR_MODE_GET|STOR_MODE_PUT;
			end;
		}
	}
	return;
}

function	script	CCCDrop	{
	.@tier = getarg(0,0);
	.@isBoss = getarg(1,0);
	// Error
	if(.@tier <= 0)
	return;
	// Body Points
	.@rand = rand(100);
	.@rate = (10 * (bonusDropMultiplier * (.@isBoss ? 2 : 1)));
	if(.@rand < .@rate){
		.@reward = (.@isBoss ? 5 : 1);
		bodyPoints[.@tier] = cap_value(bodyPoints[.@tier] + .@reward,0,INT_MAX);
		dispbottom "+" + .@reward + " แต้ม Body Tier " + .@tier + " (รวม " + F_InsertComma(bodyPoints[.@tier]) + ")",0xfafa80;
	}
	else if(#isVerboseLog)
	dispbottom "(แต้ม Body) ต้องสุ่มได้ < " + .@rate + " คุณสุ่มได้ " + .@rand,0xfa8080;
	// Trait Points
	.@rand = rand(100);
	.@rate = (5 * (bonusDropMultiplier * (.@isBoss ? 2 : 1)));
	if(.@rand < .@rate){
		.@reward = (.@isBoss ? 3 : 1);
		traitPoints[.@tier] = cap_value(traitPoints[.@tier] + .@reward,0,INT_MAX);
		dispbottom "+" + .@reward + " แต้ม Trait Tier " + .@tier + " (รวม " + F_InsertComma(traitPoints[.@tier]) + ")",0xfafa80;
	}
	else if(#isVerboseLog)
	dispbottom "(แต้ม Trait) ต้องสุ่มได้ < " + .@rate + " คุณสุ่มได้ " + .@rand,0xfa8080;
	// Skill Points
	.@rand = rand(100);
	.@rate = (3 * (bonusDropMultiplier * (.@isBoss ? 2 : 1)));
	if(.@rand < .@rate){
		.@reward = (.@isBoss ? 2 : 1);
		skillPoints[.@tier] = cap_value(skillPoints[.@tier] + .@reward,0,INT_MAX);
		dispbottom "+" + .@reward + " แต้ม Skill Tier " + .@tier + " (รวม " + F_InsertComma(skillPoints[.@tier]) + ")",0xfafa80;
	}
	else if(#isVerboseLog)
	dispbottom "(แต้ม Skill) ต้องสุ่มได้ < " + .@rate + " คุณสุ่มได้ " + .@rand,0xfa8080;
	return;
}

function	script	ReturnOrb	{
	specialeffect2 35;
	if(!IsSkipTickMap())
	progressbar "",2;
	warp "Save",0,0;
	return;
}

function	script	TeleportOrb	{
	if(!isConfirm){
		message strcharinfo(0),"สามารถใช้ได้ เมื่อเข้าไปยังในเกม CAST CAST CAST";
		end;
	}	
	specialeffect2 780;
	if(!IsSkipTickMap())
	progressbar "",1;
	warp "Random",0,0;
	return;
}

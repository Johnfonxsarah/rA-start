function	script	InputOutOfRange	{ return getarg(0,0) <= 0 || getarg(0,0) > getarg(1,0); }

function	script	IsWeekend	{ .@day = gettime(DT_DAYOFWEEK); return (((.@day == FRIDAY) && gettime(DT_HOUR) >= 18) || (.@day == SATURDAY) || (.@day == SUNDAY)); }

function	script	IsInventoryFull	{ return (!checkweight(1100,1)); }

function	script	StringBase10000	{
	.@value = getarg(0,0);
	if(.@value < 10)
	return ("0.0" + .@value);
	else if(.@value < 100)
	return ("0." + (.@value / 10));
	else if(.@value < 1000)
	return insertchar((.@value / 10),".",1);
	else
	return (.@value /= 100);
}

function	script	KM	{
	.@value = getarg(0,0);
	if(.@value < 1000)
	return .@value;
	else if(.@value < 1000000)
	return (.@value /= 1000) + "k";
	else if(.@value >= 1000000)
	return (.@value /= 1000000) + "m";
}

function	script	Cap	{ return cap_value(getarg(0,0) + getarg(1,0),0,INT_MAX); }

function	script	IsZenyExceed	{
	.@isZenyExceed = (Cap(Zeny,getarg(0,0)) > 2000000000);
	if(.@isZenyExceed)
	message strcharinfo(0),"เกิดข้อผิดพลาด: Zeny เต็มแล้ว";
	return .@isZenyExceed;
}

function	script	ChangeZeny	{
	.@value = getarg(0,0);
	.@isEffect = getarg(1,1);
	.@isIncrease = (.@value >= 0);
	Zeny = Cap(Zeny,.@value);
	message strcharinfo(0),(.@isIncrease ? "+" : "-") + F_InsertComma(.@value) + "z เป็น " + F_InsertComma(Zeny) + "z";
	if(.@isEffect)
	specialeffect2 18;
	return;
}

function	script	Storage	{
	.@menu$ = "คลังเก็บของปกติ:คลังเก็บของ Guild:";
	.@selectedStorage = select(.@menu$);
	if(.@selectedStorage == 1)
	openstorage;
	else if(.@selectedStorage == 2)
	guildopenstorage();
	return;
}

function	script	ReturnOrb	{
	warp "Save",0,0;
	return;
}

function	script	TeleportOrb	{
	warp "Random",0,0;
	return;
}

function	script	ApplyEffect	{
	bonus_script_clear 8;
	bonus_script
	"bonus2 bAddClass,CLASS_ALL," + (100 * effectLvs[0]) + ";"
	+ "bonus bSpeedAddRate," + (1 * effectLvs[8]) + ";"
	,86400,8,1;
	
	percentheal 100,100;
	return;
}

function	script	GetAvailableMap	{
	// Random the map
	.@isFoundSameMap = 1;
	.@retry = 300;
	while(.@isFoundSameMap && (.@retry > 0)){
		.@retry--;
		
		// Random map index
		.@mapIndex = rand(getarraysize($maps$));
		.@isContinue = 0;
		// Loop all level and check map available
		for(.@i = 0; .@i < getarraysize($playingMaps); .@i++){
			// Error because found same map index
			if(.@mapIndex == $playingMaps[.@i]){
				.@isContinue = 1;
				break;
			}
		}
		// Loop all level and check map available
		for(.@i = 0; .@i < getarraysize($playingBossMaps); .@i++){
			// Error because found same map index
			if(.@mapIndex == $playingBossMaps[.@i]){
				.@isContinue = 1;
				break;
			}
		}
		// Good! Found available map now
		if(!.@isContinue)
		.@isFoundSameMap = 0;
	}
	// Error..
	if(.@retry <= 0){
		mes "โปรดรอสักครู่ ขณะนี้ Map เต็ม";
		close;
	}
	return .@mapIndex;
}

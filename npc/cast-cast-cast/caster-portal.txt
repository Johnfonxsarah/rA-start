bl_depth2,141,171,5	script	PORTAL OF CASTER	10043,2,2,{
	mes "เดินผ่านเพื่อ";
	mes " สุ่ม Status";
	mes " สุ่ม Trait";
	mes " สุ่ม Skill";
	mes " สุ่ม Effect";
	mes "ตามจำนวนสุ่มสูงสุด";
	mes "Cooldown 3 นาที ต่อการเดินผ่าน";
	close;
	
OnTouch:
	if((bodyLv <= 0) && (traitLv <= 0) && (skillLv <= 0) && (effectLv <= 0)){
		mes "คุณยังไม่ได้เสริมแกร่งตัวละครใด ๆ เลย";
		close;
	}

	.@delay = 180;
	
	// Delay check
	if(pocDelay > gettimetick(2)){
		mes "โปรดรอ " + (pocDelay - gettimetick(2)) + " วินาที";
		close;
	}
	
	// Delay
	pocDelay = gettimetick(2) + .@delay;
	
	// Status
	statusup2 bStr,-5000;
	statusup2 bAgi,-5000;
	statusup2 bVit,-5000;
	statusup2 bInt,-5000;
	statusup2 bDex,-5000;
	statusup2 bLuk,-5000;
	if(bodyLv){
		statusup2 bStr,rand(bodyLv);
		statusup2 bAgi,rand(bodyLv);
		statusup2 bVit,rand(bodyLv);
		statusup2 bInt,rand(bodyLv);
		statusup2 bDex,rand(bodyLv);
		statusup2 bLuk,rand(bodyLv);
	}
	
	// Trait
	traitstatusup2 bPow,-500;
	traitstatusup2 bSta,-500;
	traitstatusup2 bWis,-500;
	traitstatusup2 bSpl,-500;
	traitstatusup2 bCon,-500;
	traitstatusup2 bCrt,-500;
	if(traitLv){
		traitstatusup2 bPow,rand(traitLv);
		traitstatusup2 bSta,rand(traitLv);
		traitstatusup2 bWis,rand(traitLv);
		traitstatusup2 bSpl,rand(traitLv);
		traitstatusup2 bCon,rand(traitLv);
		traitstatusup2 bCrt,rand(traitLv);
	}
	
	// Skill
	// Skill Amount = 1 + (Level / 5)
	// Skill cannot be same

	freeloop(1);
	
	.@skillAmount = 1;
	.@levelPerSkill = 5;
	.@bLv = BaseLevel;
	while(.@bLv >= .@levelPerSkill){
		.@bLv -= .@levelPerSkill;
		.@skillAmount++;
	}

	// Forgot old skill if available
	.@maxSkillAmount = 55;
	for(.@m = 0; .@m < .@maxSkillAmount; .@m++){
		if(skillSlots[.@m] != 99999)
		skill $offensiveSkillIds[skillSlots[.@m]],0,3;
	}
	
	.@slot = 0;
	
	while(.@skillAmount > 0){
		.@skillAmount--;
		
		// Random new skill
		skillSlots[.@slot] = rand(getarraysize($offensiveSkillIds));
		// Should not same with other slot
		while(true){
			.@isSame = 0;
			// Loop all slot
			for(.@i = 0; .@i < .@maxSkillAmount; .@i++){
				if(.@i == .@slot)
				continue;

				// Check skill was same
				if(!isSame && (skillSlots[.@slot] == skillSlots[.@i])){
					// Random new skill
					skillSlots[.@slot] = rand(getarraysize($offensiveSkillIds));
					.@isSame = 1;
					break;
				}
			}
			if(!.@isSame)
			break;
		}
		
		// Learn new skill now
		skill $offensiveSkillIds[skillSlots[.@slot]],max(1,skillLv),3;
		
		.@slot++;
	}
	
	freeloop(0);
	
	// Effect
	
	freeloop(1);
	
	.@maxEffectAmount = 14;
	
	for(.@m = 0; .@m < .@maxEffectAmount; .@m++)
	effectLvs[.@m] = 0;
	
	if(effectLv){
		for(.@m = 0; .@m < .@maxEffectAmount; .@m++)
		effectLvs[.@m] = rand(effectLv);
	}

	freeloop(0);
	
	callfunc("ApplyEffect");
	
	// Warp back to save point
	warp "Save",0,0;
	end;
	
OnInit:
	waitingroom "เดินผ่านสุ่มทุกอย่างใหม่",0;
	end;
}

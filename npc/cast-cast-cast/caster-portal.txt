bl_depth2,145,164,5	script	PORTAL OF CASTER	10043,{
	mes "จะทำการสุ่ม";
	mes " สุ่ม Status";
	mes " สุ่ม Trait";
	mes " สุ่ม Skill";
	mes " สุ่ม Effect";
	mes "ตามจำนวนสุ่มสูงสุด";
	mes "Cooldown 30~90 วินาที ต่อการใช้งาน";
	next;
	menu "ไม่ทำอะไร",-,"สุ่มใหม่",MenuRandomNow,"โกงเวลา + สุ่มใหม่ทันที (เสีย 1 Body Level และ 1 Trait Level)",MenuForceResetCooldown;
	close;
	
MenuForceResetCooldown:
	pocDelay = 0;
	bodyLv = cap_value(bodyLv - 1,0,5000);
	traitLv = cap_value(traitLv - 1,0,500);
MenuRandomNow:
	if((bodyLv <= 0) && (traitLv <= 0) && (skillLv <= 0) && (effectLv <= 0)){
		mes "คุณยังไม่ได้เสริมแกร่งตัวละครใด ๆ เลย";
		close;
	}

	.@delay = rand(30,90);
	
	// Delay check
	if(pocDelay > gettimetick(2)){
		mes "โปรดรอ " + (pocDelay - gettimetick(2)) + " วินาที";
		close;
	}
	
	// Delay
	pocDelay = gettimetick(2) + .@delay;
	
	// Status
	statusup2 bStr,-5000;
	statusup2 bAgi,-5000;
	statusup2 bVit,-5000;
	statusup2 bInt,-5000;
	statusup2 bDex,-5000;
	statusup2 bLuk,-5000;
	if(bodyLv > 0){
		.@addStr = rand(bodyLv + 1);
		.@addAgi = rand(bodyLv + 1);
		.@addVit = rand(bodyLv + 1);
		.@addInt = rand(bodyLv + 1);
		.@addDex = rand(bodyLv + 1);
		.@addLuk = rand(bodyLv + 1);
		statusup2 bStr,.@addStr;
		statusup2 bAgi,.@addAgi;
		statusup2 bVit,.@addVit;
		statusup2 bInt,.@addInt;
		statusup2 bDex,.@addDex;
		statusup2 bLuk,.@addLuk;
		dispbottom "STR: " + F_InsertComma(.@addStr)
		+ " | AGI: " + F_InsertComma(.@addAgi)
		+ " | VIT: " + F_InsertComma(.@addVit)
		+ " | INT: " + F_InsertComma(.@addInt)
		+ " | DEX: " + F_InsertComma(.@addDex)
		+ " | LUK: " + F_InsertComma(.@addLuk),0xf0c284;
	}
	
	// Trait
	traitstatusup2 bPow,-500;
	traitstatusup2 bSta,-500;
	traitstatusup2 bWis,-500;
	traitstatusup2 bSpl,-500;
	traitstatusup2 bCon,-500;
	traitstatusup2 bCrt,-500;
	if(traitLv > 0){
		.@addPow = rand(traitLv + 1);
		.@addSta = rand(traitLv + 1);
		.@addWis = rand(traitLv + 1);
		.@addSpl = rand(traitLv + 1);
		.@addCon = rand(traitLv + 1);
		.@addCrt = rand(traitLv + 1);
		traitstatusup2 bPow,.@addPow;
		traitstatusup2 bSta,.@addSta;
		traitstatusup2 bWis,.@addWis;
		traitstatusup2 bSpl,.@addSpl;
		traitstatusup2 bCon,.@addCon;
		traitstatusup2 bCrt,.@addCrt;
		dispbottom "POW: " + F_InsertComma(.@addPow)
		+ " | STA: " + F_InsertComma(.@addSta)
		+ " | WIS: " + F_InsertComma(.@addWis)
		+ " | SPL: " + F_InsertComma(.@addSpl)
		+ " | CON: " + F_InsertComma(.@addCon)
		+ " | CRT: " + F_InsertComma(.@addCrt),0x84f099;
	}
	
	// Skill
	
	// Permanent Skill
	for(.@i = 0; .@i < 8; .@i++){
		if(pSkill[.@i] != 99999){
			if(skillLv > 1)
			.@permaSkillLv[.@i] = rand(1,skillLv);
			else
			.@permaSkillLv[.@i] = 1;
			skill $offensiveSkillIds[pSkill[.@i]],.@permaSkillLv[.@i],3;
			dispbottom "Skill ถาวรช่องที่ " + (.@i + 1) + " " + $offensiveSkillDescs$[pSkill[.@i]] + " Level " + .@permaSkillLv[.@i],0x84d4f0;
		}
	}
	
	// Skill Amount = 1 + (Level / 5)
	// Skill cannot be same

	freeloop(1);
	
	.@skillAmount = 1;
	.@levelPerSkill = 5;
	.@bLv = BaseLevel;
	while(.@bLv >= .@levelPerSkill){
		.@bLv -= .@levelPerSkill;
		.@skillAmount++;
	}

	// Forgot old skill if available
	.@maxSkillAmount = 55;
	for(.@m = 0; .@m < .@maxSkillAmount; .@m++){
		if(skillSlots[.@m] != 99999)
		skill $offensiveSkillIds[skillSlots[.@m]],0,3;
		skillSlots[.@m] = 99999;
	}
	
	.@slot = 0;
	
	while(.@skillAmount > 0){
		.@skillAmount--;
		
		// Random new skill
		skillSlots[.@slot] = callsub(GetNewSkillId);
		
		// Should not same with other slot
		while(true){
			.@isSame = 0;
			
			// Loop all slot
			for(.@i = 0; .@i < .@maxSkillAmount; .@i++){
				if(.@i == .@slot)
				continue;
				
				// Check skill was same
				if(!isSame && (skillSlots[.@slot] == skillSlots[.@i])){
					// Random new skill
					skillSlots[.@slot] = callsub(GetNewSkillId);
					.@isSame = 1;
					break;
				}
			}
			
			if(!.@isSame)
			break;
		}

		// Learn new skill now
		skill $offensiveSkillIds[skillSlots[.@slot]],max(1,skillLv),3;
		dispbottom "Skill สุ่มได้ช่องที่ " + (.@slot + 1) + " " + $offensiveSkillDescs$[skillSlots[.@slot]] + " Level " + max(1,skillLv),0x84f0ed;
		
		.@slot++;
	}
	
	freeloop(0);
	
	// Effect
	
	freeloop(1);
	
	.@maxEffectAmount = 17;
	
	for(.@m = 0; .@m < .@maxEffectAmount; .@m++)
	effectLvs[.@m] = 0;

	if(effectLv > 0){
		for(.@m = 0; .@m < .@maxEffectAmount; .@m++)
		effectLvs[.@m] = rand(effectLv + 1);
	}
	
	.@maxHpLv = effectLvs[9];
	.@baseMaxHp =
	(.@maxHpLv < 10) ? 1000
	: (.@maxHpLv < 20) ? 2500
	: (.@maxHpLv < 30) ? 4000
	: (.@maxHpLv < 40) ? 5500
	: (.@maxHpLv < 50) ? 7500
	: (.@maxHpLv < 60) ? 9500
	: (.@maxHpLv < 70) ? 11500
	: (.@maxHpLv < 80) ? 14000
	: (.@maxHpLv < 90) ? 16500
	: 20000;
	dispbottom "[Lv. " + effectLvs[0] + "] โจมตีกายภาพ +" + (100 * effectLvs[0]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[1] + "] โจมตีเวทย์ +" + (100 * effectLvs[1]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[2] + "] เจาะ DEF +" + (1 * effectLvs[2]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[3] + "] เจาะ MDEF +" + (1 * effectLvs[3]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[4] + "] กันกายภาพ +" + (1 * effectLvs[4]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[5] + "] กันเวทย์ +" + (1 * effectLvs[5]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[6] + "] ร่ายเร็วขึ้น +" + (1 * effectLvs[6]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[7] + "] ลด Cooldown +" + (1 * effectLvs[7]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[8] + "] เคลื่อนที่เร็วขึ้น +" + (1 * effectLvs[8]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[9] + "] พลังชีวิต +" + (.@baseMaxHp * effectLvs[9]) + "",0xedf084;
	dispbottom "[Lv. " + effectLvs[10] + "] กันการสะท้อน +" + (1 * effectLvs[10]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[11] + "] กันสถานะผิดปกติ +" + (1 * effectLvs[11]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[12] + "] เจาะ RES +" + (1 * effectLvs[12]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[13] + "] เจาะ MRES +" + (1 * effectLvs[13]) + "%",0xedf084;
	dispbottom "[Lv. " + effectLvs[14] + "] ฟื้นฟู HP " + (effectLvs[14] ? 1 : 0) + "% ทุก ๆ +" + ((11000 - cap_value(100 * effectLvs[14],0,10000)) / 1000) + " วินาที",0xedf084;
	dispbottom "[Lv. " + effectLvs[15] + "] ATK +^9300ff" + (10 * effectLvs[15]),0xedf084;
	dispbottom "[Lv. " + effectLvs[16] + "] MATK +^9300ff" + (10 * effectLvs[16]),0xedf084;
	
	freeloop(0);
	
	callfunc("ApplyEffect");
	
	// Warp back to save point
	warp "Save",0,0;
	
	end;
	
GetNewSkillId:
	setarray .@errorSkillIds[0],5350,5338,408,271,5260,5255,45,2234;
	
	.@workingSkillId = rand(getarraysize($offensiveSkillIds));
	
	while(true){
		.@isBadSkillId = 0;
		
		// Check for error skill
		for(.@bad = 0; .@bad < getarraysize(.@errorSkillIds); .@bad++){
			if(.@workingSkillId == .@errorSkillIds[.@bad]){
				// Random new skill
				.@workingSkillId = rand(getarraysize($offensiveSkillIds));
				.@isBadSkillId = 1;
				break;
			}
		}
		
		if(!.@isBadSkillId)
		break;
	}
	
	return .@workingSkillId;
	
OnInit:
	waitingroom "สุ่มทุกอย่างใหม่",0;
	end;
}

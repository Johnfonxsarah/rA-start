bl_depth2,132,159,6	script	หินผนึกพลัง	10371,{
OnMenu:
	.@maxEffectLv = 100;
	.@availablePoint = ((BaseLevel >= spendLv) ? (BaseLevel - spendLv) : 0);
	mes "คุณมี Level: " + BaseLevel;
	mes "สามารถเพิ่มพลังได้อีก: " + .@availablePoint + " ขั้น";
	next;
	.@effect = (select("[Lv. ^1c9311" + effectLvs[0] + "^000000] โจมตีกายภาพ +^9300ff" + (100 * effectLvs[0]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[1] + "^000000] โจมตีเวทย์ +^9300ff" + (100 * effectLvs[1]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[2] + "^000000] เจาะ DEF +^9300ff" + (1 * effectLvs[2]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[3] + "^000000] เจาะ MDEF +^9300ff" + (1 * effectLvs[3]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[4] + "^000000] กันกายภาพ +^9300ff" + (1 * effectLvs[4]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[5] + "^000000] กันเวทย์ +^9300ff" + (1 * effectLvs[5]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[6] + "^000000] ร่ายเร็วขึ้น +^9300ff" + (1 * effectLvs[6]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[7] + "^000000] ลด Cooldown +^9300ff" + (1 * effectLvs[7]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[8] + "^000000] เคลื่อนที่เร็วขึ้น +^9300ff" + (1 * effectLvs[8]) + "%^000000"
	+ ":[Lv. ^1c9311" + effectLvs[9] + "^000000] พลังชีวิต +^9300ff" + (1000 * effectLvs[9]) + "^000000"
	+ ":[Lv. ^1c9311" + effectLvs[10] + "^000000] กันการสะท้อน +^9300ff" + (1 * effectLvs[10]) + "%^000000"
	+ ":Reset การผนึกพลัง") - 1);
	if(.@effect == 11){
		menu "ยกเลิก",-,"ยืนยัน (Level -5, All Status -50, All Trait -25, สุ่มลืม Skill 1 อย่าง)",MenuResetEffect;
		close;
MenuResetEffect:
		BaseLevel -= 5;
		statusup2 bStr,-50;
		statusup2 bAgi,-50;
		statusup2 bVit,-50;
		statusup2 bInt,-50;
		statusup2 bDex,-50;
		statusup2 bLuk,-50;
		traitstatusup2 bPow,-25;
		traitstatusup2 bSta,-25;
		traitstatusup2 bWis,-25;
		traitstatusup2 bSpl,-25;
		traitstatusup2 bCon,-25;
		traitstatusup2 bCrt,-25;
		// Skill Penalty (Remove 1 Skill, Minimum 1 skill left)
		.@learnSkillCount = 0;
		freeloop(1);
		for(.@i = 0; .@i < 9; .@i++){
			if(skillSlots[.@i] != 99999)
			.@learnSkillCount++;
		}
		if(.@learnSkillCount > 1){
			.@isDoneRemoveSkill = 0;
			while(!.@isDoneRemoveSkill){
				.@randomLostSkillSlot = rand(9);
				if(skillSlots[.@randomLostSkillSlot] != 99999){
					skill $offensiveSkillIds[skillSlots[.@randomLostSkillSlot]],0,3;
					skillSlots[.@randomLostSkillSlot] = 99999;
					.@isDoneRemoveSkill = 1;
				}
			}
		}
		freeloop(0);
		for(.@i = 0; .@i < 11; .@i++)
		effectLvs[.@i] = 0;
		spendLv = 0;
		callfunc("ApplyCCCRules");
		close;
	}
	if((.@availablePoint <= 0) || (effectLvs[.@effect] >= .@maxEffectLv)){
		mes "ยกเลิกเรียบร้อยแล้ว";
		close;
	}
	.@maxInput = ((.@availablePoint + effectLvs[.@effect]) > .@maxEffectLv) ? (.@maxEffectLv - effectLvs[.@effect]) : .@availablePoint;
	mes "โปรดระบุ 1~" + .@maxInput;
	next;
	input .@i;
	if((.@i <= 0) || (.@i > .@maxInput)){
		mes "ยกเลิกเรียบร้อยแล้ว";
		close;
	}
	effectLvs[.@effect] += .@i;
	spendLv += .@i;
	callfunc("ApplyEffect");
	specialeffect2 734;
	goto OnMenu;
	
OnInit:
	waitingroom "เพิ่งพลัง (ถาวร)",0;
	end;
}

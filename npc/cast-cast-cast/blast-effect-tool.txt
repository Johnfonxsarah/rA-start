bl_depth2,132,159,6	script	หินผนึกพลัง BLAST	10371,{
OnMenu:
	.@maxHpLv = blastEffectLvs[9];
	.@baseMaxHp =
	(.@maxHpLv < 10) ? 1000
	: (.@maxHpLv < 20) ? 2500
	: (.@maxHpLv < 30) ? 4000
	: (.@maxHpLv < 40) ? 5500
	: (.@maxHpLv < 50) ? 7500
	: (.@maxHpLv < 60) ? 9500
	: (.@maxHpLv < 70) ? 11500
	: (.@maxHpLv < 80) ? 14000
	: (.@maxHpLv < 90) ? 16500
	: 20000;
	.@maxEffectLv = 100;
	.@availablePoint = ((blastLv >= blastSpendLv) ? (blastLv - blastSpendLv) : 0);
	mes "คุณมี BLAST Level: " + blastLv;
	mes "สามารถเพิ่มพลังได้อีก: " + .@availablePoint + " ขั้น";
	next;
	.@effect = (select("[Lv. ^1c9311" + blastEffectLvs[0] + "^000000] โจมตีกายภาพ +^9300ff" + (100 * blastEffectLvs[0]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[1] + "^000000] โจมตีเวทย์ +^9300ff" + (100 * blastEffectLvs[1]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[2] + "^000000] เจาะ DEF +^9300ff" + (1 * blastEffectLvs[2]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[3] + "^000000] เจาะ MDEF +^9300ff" + (1 * blastEffectLvs[3]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[4] + "^000000] กันกายภาพ +^9300ff" + (1 * blastEffectLvs[4]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[5] + "^000000] กันเวทย์ +^9300ff" + (1 * blastEffectLvs[5]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[6] + "^000000] ร่ายเร็วขึ้น +^9300ff" + (1 * blastEffectLvs[6]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[7] + "^000000] ลด Cooldown +^9300ff" + (1 * blastEffectLvs[7]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[8] + "^000000] เคลื่อนที่เร็วขึ้น +^9300ff" + (1 * blastEffectLvs[8]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[9] + "^000000] พลังชีวิต +^9300ff" + (.@baseMaxHp * blastEffectLvs[9]) + "^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[10] + "^000000] กันการสะท้อน +^9300ff" + (1 * blastEffectLvs[10]) + "%^000000"
	+ ":[Lv. ^1c9311" + blastEffectLvs[11] + "^000000] กันสถานะผิดปกติ +^9300ff" + (1 * blastEffectLvs[11]) + "%^000000"
	+ ":Reset การผนึกพลัง") - 1);
	if(.@effect == 12){
		menu "ยกเลิก",-,"ยืนยัน (All Status -5000, All Trait -2500, สุ่มลืม Skill 10 อย่าง)",MenuResetEffect;
		close;
MenuResetEffect:
		callfunc("DeadPenalty",100,100,10);
		for(.@i = 0; .@i < 12; .@i++)
		blastEffectLvs[.@i] = 0;
		blastSpendLv = 0;
		callfunc("ApplyCCCRules");
		close;
	}
	if((.@availablePoint <= 0) || (blastEffectLvs[.@effect] >= .@maxEffectLv)){
		mes "ยกเลิกเรียบร้อยแล้ว";
		close;
	}
	.@maxInput = ((.@availablePoint + blastEffectLvs[.@effect]) > .@maxEffectLv) ? (.@maxEffectLv - blastEffectLvs[.@effect]) : .@availablePoint;
	mes "โปรดระบุ 1~" + .@maxInput;
	next;
	input .@i;
	if((.@i <= 0) || (.@i > .@maxInput)){
		mes "ยกเลิกเรียบร้อยแล้ว";
		close;
	}
	blastEffectLvs[.@effect] += .@i;
	blastSpendLv += .@i;
	callfunc("ApplyEffect");
	specialeffect2 734;
	goto OnMenu;
	
OnInit:
	waitingroom "เพิ่งพลัง BLAST (ถาวร)",0;
	end;
}

bl_depth2,141,137,5	script	เล่นเกม	10558,{
	callfunc("RemovePlayDelegates");
	
	percentheal 100,100;
	mes "โปรดระบุชั้นที่ต้องการเข้าเล่น";
	mes "1~" + BaseLevel;
	next;
	input .@lv;
	if(InputOutOfRange(.@lv,BaseLevel)){
		mes "ยกเลิกเรียบร้อยแล้ว";
		close;
	}
	
	freeloop(1);
	
	// Check map index existed on those level
	// Just join
	if($playingMaps[.@lv])
	goto OnJoin;
	
	// Random the map
	.@isFoundSameMap = 1;
	.@retry = 300;
	while(.@isFoundSameMap && (.@retry > 0)){
		// Random map index
		.@mapIndex = rand(getarraysize($maps$));
		.@isContinue = 0;
		// Loop all level and check map available
		for(.@i = 0; .@i < getarraysize($playingMaps); .@i++){
			// Error because found same map index
			if(.@mapIndex == $playingMaps[.@i]){
				.@isContinue = 1;
				break;
			}
		}
		// Good! Found available map now
		if(!.@isContinue)
		.@isFoundSameMap = 0;
	}
	// Error..
	if(.@retry <= 0){
		mes "โปรดรอสักครู่ ขณะนี้ Map เต็ม";
		close;
	}
	// Set playing map index
	$playingMaps[.@lv] = .@mapIndex;
OnJoin:
	// Remember playing floor
	lastPlayMap = .@lv;
	bonusDropMultiplier = lastPlayMap;
	while(bonusDropMultiplier > 10)
	bonusDropMultiplier -= 10;
	// Remember last map name
	lastPlayMap$ = $maps$[$playingMaps[.@lv]];
	// Remember monster id
	tier =
	(lastPlayMap <= 5) ? 1
	: (lastPlayMap <= 10) ? 2
	: (lastPlayMap <= 20) ? 3
	: (lastPlayMap <= 30) ? 4
	: (lastPlayMap <= 40) ? 5
	: (lastPlayMap <= 50) ? 6
	: (lastPlayMap <= 60) ? 7
	: (lastPlayMap <= 70) ? 8
	: (lastPlayMap <= 80) ? 9
	: (lastPlayMap <= 90) ? 10
	: 11
	;
	if(tier >= 11){
		.@arraySize = getarraysize($mvpIds);
		setarray monsterIds[0],$mvpIds[rand(.@arraySize)];
		setarray monsterIds[1],$mvpIds[rand(.@arraySize)];
		setarray monsterIds[2],$mvpIds[rand(.@arraySize)];
	}
	else{
		.@arraySize = getarraysize(getd("$attackableMonsterTier" + tier + "Ids"));
		setarray monsterIds[0],getd("$attackableMonsterTier" + tier + "Ids[" + rand(.@arraySize) + "]");
		setarray monsterIds[1],getd("$attackableMonsterTier" + tier + "Ids[" + rand(.@arraySize) + "]");
		setarray monsterIds[2],getd("$attackableMonsterTier" + tier + "Ids[" + rand(.@arraySize) + "]");
	}
	
	freeloop(0);
	
	// Remember base spawn amount
	baseSpawnAmount =
	(tier <= 2) ? 2
	: (tier <= 4) ? 3
	: (tier <= 6) ? 4
	: (tier <= 8) ? 5
	: 6
	;
	// Remember spawn timer multiplier
	spawnTimerMultiplier = 10;
	
	callfunc("ApplyCCCRules");
	
	// Warp now
	warp $maps$[$playingMaps[lastPlayMap]],0,0;
	// About to start spawn monster around player
	hintTimer = 3;
	addtimer 2000,strnpcinfo(0) + "::OnSpawnMonsterHint";
	close;
	
OnSpawnMonsterHint:
	// Check map still same otherwise stop it. || Player not attached || Boss Fight || Player dead
	if((lastPlayMap$ != strcharinfo(3)) || !playerattached() || $isBossMaps[lastPlayMap] || isdead()){
		// Mark on minimap
		if(playerattached() && $isBossMaps[lastPlayMap])
		viewpoint 1,$bossX[lastPlayMap],$bossY[lastPlayMap],1,0xff0000;
		
		end;
	}
	
	if(hintTimer){
		announce "สาวกมนต์ดำ จะมาภายใน " + hintTimer + " วินาที",bc_self,0x9e978b,0,25;
		hintTimer--;
		addtimer 1000,strnpcinfo(0) + "::OnSpawnMonsterHint";
	}
	else
	addtimer 1,strnpcinfo(0) + "::OnSpawnMonster";
	end;
	
	// Keep spawn around players
OnSpawnMonster:
	// Check map still same otherwise stop it. || Player not attached || Boss Fight || Player dead
	if((lastPlayMap$ != strcharinfo(3)) || !playerattached() || $isBossMaps[lastPlayMap] || isdead())
	end;
	// Spawn amount: Base ~ (Base ^ 2)
	.@spawnAmount = rand(baseSpawnAmount,pow(baseSpawnAmount,2));
	// Get player cell
	getmapxy(.@cMap$,.@cX,.@cY);
	freeloop(1);
	for(.@i = 0; .@i < .@spawnAmount; .@i++){
		// Get available cell to spawn around player
		getfreecell .@cMap$,.@rX,.@rY,.@cX,.@cY,rand(5,15),rand(5,15);
		// Spawn
		monster .@cMap$,.@rX,.@rY,"สาวกมนต์ดำ",monsterIds[rand(getarraysize(monsterIds))],1,strnpcinfo(0) + "::OnMonsterDead";
		.GID = $@mobid[0];
		setunitdata .GID,UMOB_LEVEL,lastPlayMap;
		.@mHP = (10 * pow(lastPlayMap,3));
		setunitdata .GID,UMOB_MAXHP,.@mHP;
		setunitdata .GID,UMOB_HP,.@mHP;
		.@bothSpeed = (500 - (cap_value(lastPlayMap * 5,1,350)));
		setunitdata .GID,UMOB_SPEED,.@bothSpeed;
		setunitdata .GID,UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CANATTACK|MD_TELEPORTBLOCK|MD_DETECTOR;
		setunitdata .GID,UMOB_STR,rand(lastPlayMap,lastPlayMap * 2);
		setunitdata .GID,UMOB_AGI,rand(lastPlayMap,lastPlayMap * 2);
		setunitdata .GID,UMOB_VIT,rand(lastPlayMap,lastPlayMap * 2);
		setunitdata .GID,UMOB_INT,rand(lastPlayMap,lastPlayMap * 2);
		setunitdata .GID,UMOB_DEX,rand(lastPlayMap,lastPlayMap * 2);
		setunitdata .GID,UMOB_LUK,rand(lastPlayMap,lastPlayMap * 2);
		setunitdata .GID,UMOB_DMGIMMUNE,0;
		//setunitdata .GID,UMOB_ATKRANGE,1;
		setunitdata .GID,UMOB_ATKMIN,5 + lastPlayMap;
		setunitdata .GID,UMOB_ATKMAX,10 + lastPlayMap;
		setunitdata .GID,UMOB_MATKMIN,5 + lastPlayMap;
		setunitdata .GID,UMOB_MATKMAX,10 + lastPlayMap;
		setunitdata .GID,UMOB_DEF,5 + lastPlayMap;
		setunitdata .GID,UMOB_MDEF,5 + lastPlayMap;
		setunitdata .GID,UMOB_AMOTION,.@bothSpeed;
		setunitdata .GID,UMOB_ADELAY,.@bothSpeed;
		setunitdata .GID,UMOB_DMOTION,.@bothSpeed;
		setunitdata .GID,UMOB_RES,lastPlayMap;
		setunitdata .GID,UMOB_MRES,lastPlayMap;
		setunitdata .GID,UMOB_DAMAGETAKEN,100;
		//setunitdata $@mobid[0],UMOB_RANK,0;
	}
	freeloop(0);
	// Start next spawn timer
	addtimer (1000 * rand(5,spawnTimerMultiplier)),strnpcinfo(0) + "::OnSpawnMonster";
	end;
	
OnMonsterDead:
	// Check map still same otherwise stop it. || Player not attached || Boss Fight || Player dead
	if((lastPlayMap$ != strcharinfo(3)) || !playerattached() || $isBossMaps[lastPlayMap] || isdead())
	end;
	.@scoreRequired = 300;
	$scoreMaps[lastPlayMap]++;
	callfunc("CCCDrop",tier);
	if($scoreMaps[lastPlayMap] >= .@scoreRequired){
		$scoreMaps[lastPlayMap] = 0;
		$isBossMaps[lastPlayMap] = 1;
		killmonsterall strcharinfo(3),0;
		announce "เจ้าแห่งมนต์ดำ ปรากฎแล้ว! (เช็ค Minimap)",bc_map,0x9e978b,0,25;
		// Get available cell to spawn
		getfreecell strcharinfo(3),$bossX[lastPlayMap],$bossY[lastPlayMap];
		// Spawn
		monster strcharinfo(3),$bossX[lastPlayMap],$bossY[lastPlayMap],"เจ้าแห่งมนต์ดำ",monsterIds[rand(getarraysize(monsterIds))],1,strnpcinfo(0) + "::OnBossDead",Size_Large;
		.GID = $@mobid[0];
		setunitdata .GID,UMOB_LEVEL,lastPlayMap;
		.@mHP = 10 * pow(lastPlayMap,4);
		setunitdata .GID,UMOB_MAXHP,.@mHP;
		setunitdata .GID,UMOB_HP,.@mHP;
		.@bothSpeed = (500 - (cap_value(lastPlayMap * 5,1,350)));
		setunitdata .GID,UMOB_SPEED,.@bothSpeed;
		setunitdata .GID,UMOB_MODE,MD_CANMOVE|MD_AGGRESSIVE|MD_CANATTACK|MD_TELEPORTBLOCK|MD_DETECTOR;
		setunitdata .GID,UMOB_STR,rand(lastPlayMap * 5,lastPlayMap * 10);
		setunitdata .GID,UMOB_AGI,rand(lastPlayMap * 5,lastPlayMap * 10);
		setunitdata .GID,UMOB_VIT,rand(lastPlayMap * 5,lastPlayMap * 10);
		setunitdata .GID,UMOB_INT,rand(lastPlayMap * 5,lastPlayMap * 10);
		setunitdata .GID,UMOB_DEX,rand(lastPlayMap * 5,lastPlayMap * 10);
		setunitdata .GID,UMOB_LUK,rand(lastPlayMap * 5,lastPlayMap * 10);
		setunitdata .GID,UMOB_DMGIMMUNE,0;
		//setunitdata .GID,UMOB_ATKRANGE,1;
		setunitdata .GID,UMOB_ATKMIN,(5 + lastPlayMap) * 2;
		setunitdata .GID,UMOB_ATKMAX,(10 + lastPlayMap) * 2;
		setunitdata .GID,UMOB_MATKMIN,(5 + lastPlayMap) * 2;
		setunitdata .GID,UMOB_MATKMAX,(10 + lastPlayMap) * 2;
		setunitdata .GID,UMOB_DEF,(5 + lastPlayMap) * 2;
		setunitdata .GID,UMOB_MDEF,(5 + lastPlayMap) * 2;
		setunitdata .GID,UMOB_AMOTION,.@bothSpeed;
		setunitdata .GID,UMOB_ADELAY,.@bothSpeed;
		setunitdata .GID,UMOB_DMOTION,.@bothSpeed;
		setunitdata .GID,UMOB_RES,lastPlayMap * 2;
		setunitdata .GID,UMOB_MRES,lastPlayMap * 2;
		setunitdata .GID,UMOB_DAMAGETAKEN,100;
		//setunitdata $@mobid[0],UMOB_RANK,0;
		addrid(1);
		// Mark on minimap
		viewpoint 1,$bossX[lastPlayMap],$bossY[lastPlayMap],1,0xff0000;
		end;
	}
	else
	announce "สาวกมนต์ดำ เหลือ " + (.@scoreRequired - $scoreMaps[lastPlayMap]) + " ตัว",bc_map,0x9e978b,0,25;
	end;
	
OnBossDead:
	if(playerattached()){
		$isBossMaps[lastPlayMap] = 0;
		callfunc("CCCDrop",tier,1);
		announce strcharinfo(0) + " กำจัดเจ้าแห่งมนต์ดำ! ทุกคนในชั้น " + lastPlayMap + " สามารถไปยังชั้นต่อไปได้แล้ว",bc_all,0xffff00,0,25;
	}
	addrid(1);
	if((BaseLevel - 1) < lastPlayMap)
	atcommand "@lvup " + (lastPlayMap - (BaseLevel - 1));

	callfunc("ApplyCCCRules");
	
	menu "เล่นต่อ",-,"กลับไปยังห้องพัก",L_Lobby;
	hintTimer = 3;
	addtimer 2000,strnpcinfo(0) + "::OnSpawnMonsterHint";
	end;
	
	L_Lobby:
	warp "Save",0,0;
	end;
	
OnInit:
	// Clean all playing map
	deletearray $playingMaps[0],getarraysize($playingMaps);
	// Clean all boss remember
	deletearray $isBossMaps[0],getarraysize($isBossMaps);
	
	// Map database
	if($mapVersion != 1){
		$mapVersion = 1;
		
		deletearray $maps$[0],getarraysize($maps$);
		setarray $maps$[0],"gef_fild00","gef_fild01","gef_fild02","gef_fild03","gef_fild04","gef_fild05","gef_fild06","gef_fild07","gef_fild08","gef_fild09","gef_fild10","gef_fild11","gef_fild12","gef_fild13","gef_fild14","moc_fild01","moc_fild02","moc_fild03","moc_fild07","moc_fild11","moc_fild12","moc_fild13","moc_fild16","moc_fild17","moc_fild18","moc_fild19","pay_fild01","pay_fild02","pay_fild03","pay_fild04","pay_fild05","pay_fild06","pay_fild07","pay_fild08","pay_fild09","pay_fild10","pay_fild11","prt_fild00","prt_fild01","prt_fild02","prt_fild03","prt_fild04","prt_fild05","prt_fild06","prt_fild07","prt_fild08","prt_fild09","prt_fild10","prt_fild11","xmas_fild01","cmd_fild01","cmd_fild02","cmd_fild03","cmd_fild04","cmd_fild05","cmd_fild06","cmd_fild07","cmd_fild08","cmd_fild09","yuno_fild01","yuno_fild02","yuno_fild03","yuno_fild04","ama_fild01","gon_fild01","um_fild01","um_fild02","um_fild03","um_fild04","nif_fild01","nif_fild02","lou_fild01","ayo_fild01","ayo_fild02","yuno_fild05","yuno_fild07","yuno_fild08","yuno_fild09","yuno_fild11","yuno_fild12","ein_fild06","ein_fild07","ein_fild08","ein_fild09","ein_fild10","ein_fild03","ein_fild04","lhz_fild02","lhz_fild03","lhz_fild01","hu_fild07","hu_fild05","hu_fild04","hu_fild01","yuno_fild06","hu_fild02","hu_fild03","hu_fild06","ein_fild01","ein_fild02","ein_fild05","yuno_fild10","ra_fild01","ra_fild02","ra_fild03","ra_fild04","ra_fild05","ra_fild06","ra_fild07","ra_fild08","ra_fild09","ra_fild10","ra_fild11","ra_fild12","ra_fild13","ve_fild01","ve_fild02","ve_fild03","ve_fild04","ve_fild05","ve_fild07","mosk_fild02","moc_fild20","moc_fild21","moc_fild22","man_fild01","man_fild03","spl_fild02","spl_fild03","moc_fild22b","man_fild02","spl_fild01","bra_fild01","dic_fild01","dic_fild02","bif_fild01","bif_fild02","dew_fild01","ma_fild01","ma_fild02","prt_fild08a","prt_fild08b","prt_fild08c","prt_fild08d","lasa_fild01","lasa_fild02","tra_fild","gw_fild01","gw_fild02";
	}
	end;
}
